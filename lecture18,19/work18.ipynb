{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "59fafeba-f2ff-403e-b261-186ef5ab1262",
   "metadata": {},
   "source": [
    "\n",
    "## White dwarfs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71bb5e31-dd3e-4cde-8ff6-4a9c20c82e39",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "url = \"https://cdsarc.u-strasbg.fr/viz-bin/asu-tsv?-source=J/A+A/420/507/tablea1&-out=Mass&-out=Rad\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "445ff36f-d2fd-4796-a28d-3f9d8bc4f420",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "catalog = download(url)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e086817c-2c16-4cda-81af-8e1454ed09ee",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using CSV\n",
    "using DataFrames"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "432080d1-ef73-4d01-a71e-e42ab7e6983f",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "df = CSV.read(catalog, DataFrame, comment=\"#\", skipto=4, types=[Float64, Float64],\n",
    "    delim='\\t', ignorerepeated=true, silencewarnings=true);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9018196f-c54c-44e0-9769-be781c072190",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "dropmissing!(df);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1121858e-bd28-486f-95dd-9aaf29fa518b",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using PyPlot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "68764ca8-763b-4f33-938d-b56280d8a2a0",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(df.Mass, df.Rad, marker=\"o\", linestyle=\"none\",\n",
    "    fillstyle=\"none\", markersize=4, label=\"observations\")\n",
    "title(\"Radius vs. Mass for White Dwarfs\")\n",
    "xlabel(\"Mass (Solar Masses)\")\n",
    "ylabel(\"Radius (Solar Radii)\")\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "40821cc0-e883-4c6c-bcf6-32149370d3a6",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using OrdinaryDiffEqTsit5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d16e4ddf-19ad-4782-bca2-c012c9ac08e5",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "    white_dwarf_eqs!(dudr, u, p, r)\n",
    "\n",
    "The right hand side of the system of 2 dimensionless differential \n",
    "equations describing the radial distribution of the density, rho(r),\n",
    "and mass, m(r), inside a white dwarf star\n",
    "\n",
    "     m' =  rho r^2\n",
    "   rho' = -m rho /(gamma(\\rho) r^2)\n",
    "\n",
    "where gamma(rho) = rho^(2/3)/(3 sqrt(1 + rho^(2/3)))\n",
    "\"\"\"\n",
    "function white_dwarf_eqs!(dudr, u, rho_c, r)\n",
    "    rho = u[2]\n",
    "    if rho < 0\n",
    "        dudr[1] = 0\n",
    "        dudr[2] = 0\n",
    "        return\n",
    "    end\n",
    "    \n",
    "    m = u[1]\n",
    "    r2 = r ^ 2\n",
    "    dudr[1] = rho * r2\n",
    "    \n",
    "    w = rho ^ (2/3)\n",
    "    gamma = w / (3 * sqrt(1 + w))\n",
    "    \n",
    "    if r < 1e-6\n",
    "        dudr[2] = -rho_c * r * rho / (3 * gamma)\n",
    "        return\n",
    "    end\n",
    "    dudr[2] = -m * rho / (gamma * r2)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b7a8d10b-7ee3-4788-9257-596f3432d90f",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "rspan = (0.0, 10.0)\n",
    "condition(u, t, integrator) = u[2]\n",
    "affect!(integrator) = terminate!(integrator)\n",
    "cb = ContinuousCallback(condition, affect!);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d3a5a532-7288-4113-ad81-4e68e8f51e6d",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "rho_c0 = 1\n",
    "u0_particular = [0.0, rho_c0]\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "eb765dd6-84e1-4d31-bb1c-82c8922accd8",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "prob = ODEProblem(white_dwarf_eqs!, u0_particular, rspan, rho_c0)\n",
    "sol = solve(prob, Tsit5(), callback=cb);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "27313b7b-0fc0-4bc4-8a6b-46d98b050307",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "rad_particular = sol.t\n",
    "mass_particular = sol[1, :]\n",
    "rho_particular = sol[2, :]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1870ab4d-51c6-4ffa-9988-b93edcc02cb8",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(rad_particular, rho_particular)\n",
    "title(\"Density of White Dwarf\")\n",
    "xlabel(\"Distance from Center (arb. units)\")\n",
    "ylabel(\"Density (arb. units)\")\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "099cb7cd-dc4b-41c6-bc13-ab0bccd9a4fc",
   "metadata": {},
   "source": [
    "$$ \\vec{F_g} = -\\frac{GMm}{r^2}\\hat{r} \\implies g = G \\frac{M}{r^2} $$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c74b95d0-9c27-45df-a7c6-9468c2004870",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "skipto = 2\n",
    "plot(rad_particular[skipto:end], mass_particular[skipto:end] .* rad_particular[skipto:end] .^ (-2))\n",
    "title(\"Gravity Strength vs. Distance from Center\")\n",
    "xlabel(\"Distance from Center (arb. units)\")\n",
    "ylabel(\"Gravity Strength (arb. units)\")\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2e8494b7-ae9e-4b5b-a1ec-2e43cde311c5",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(\n",
    "    rad_particular[skipto:end], \n",
    "    mass_particular[skipto:end] .* rho_particular[skipto:end] .* rad_particular[skipto:end] .^ (-2)\n",
    ")\n",
    "title(\"Weight per Unit Volume vs. Distance from Center\")\n",
    "xlabel(\"Distance from Center (arb. units)\")\n",
    "ylabel(\"Weight per Unit Volume (arb. units)\")\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b8646e05-0a41-4769-a9ba-49b78e543897",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "rhocbegin = 0.08\n",
    "rhocend = 1e5\n",
    "np = 100\n",
    "\n",
    "rhocs = exp.(range(log(rhocbegin), log(rhocend), np));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "09b96d7d-2ee3-460c-95a1-e06d290d5fd1",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "radii = zeros(np)\n",
    "masses = zeros(np);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dc2d1434-45a1-4fb0-b076-d441dc9ecf15",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "for i = 1:np\n",
    "    rho_c = rhocs[i]\n",
    "    u0 = [0, rho_c]\n",
    "    \n",
    "    prob = ODEProblem(white_dwarf_eqs!, u0, rspan, rho_c)\n",
    "    sol = solve(prob, Tsit5(), callback=cb);\n",
    "\n",
    "    radii[i] = sol.t[end]\n",
    "    masses[i] = sol.u[end][1]\n",
    "end\n",
    "\n",
    "# convert to solar radii and solar masses\n",
    "radii .*= 0.006\n",
    "masses .*= 0.71;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6c4e0742-9f5c-4170-9ad2-5f589f17d10f",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(masses, radii, label=\"theory\")\n",
    "plot(df.Mass, df.Rad, marker=\"o\", linestyle=\"none\",\n",
    "    fillstyle=\"none\", markersize=4, label=\"observations\")\n",
    "title(\"Radius vs. Mass for White Dwarfs\")\n",
    "xlabel(\"Mass (Solar Masses)\")\n",
    "ylabel(\"Radius (Solar Radii)\")\n",
    "grid(true)\n",
    "legend()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3934c461-27af-4734-ab69-8b9d9c3f7b2d",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.6",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
